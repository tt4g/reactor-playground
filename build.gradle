plugins {
    id "java-library"
}

ext {
    versions = [:]
}

versions.junitJupiterVersion = "5.6.2"
versions.junitPlatformLauncherVersion = "1.6.2"
versions.mockitoVersion = "3.5.10"
versions.assertjVersion = "3.17.1"
versions.projectReactorToolsblockhoundVersion = "1.0.4.RELEASE"

allprojects {
    group = "com.github.tt4g.reactor.playground"
    version = "0.1.0"

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.release = 11
        options.encoding = "UTF-8"
    }
}

dependencies {
    implementation(platform(group: "io.projectreactor", name: "reactor-bom", version: "Dysprosium-RELEASE"))
    implementation(group: "io.projectreactor", name: "reactor-core")

    testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: versions.junitJupiterVersion)
    testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: versions.junitJupiterVersion)
    testRuntimeOnly(group: "org.junit.platform", name: "junit-platform-launcher", version: versions.junitPlatformLauncherVersion)

    testImplementation(group: "org.mockito", name: "mockito-core", version: versions.mockitoVersion)
    testImplementation(group: "org.mockito", name: "mockito-junit-jupiter", version: versions.mockitoVersion)

    testImplementation(group: "org.assertj", name: "assertj-core", version: versions.assertjVersion)

    testImplementation(group: "io.projectreactor", name: "reactor-test")
    testImplementation(group: "io.projectreactor.tools", name: "blockhound", version: versions.projectReactorToolsblockhoundVersion)
    testImplementation(group: "io.projectreactor.tools", name: "blockhound-junit-platform", version: versions.projectReactorToolsblockhoundVersion)
}

test {
    useJUnitPlatform()

    // Avoid JDK 13 and above with Reactor BlockHound problem.
    // See: https://github.com/reactor/BlockHound/issues/33
    if(JavaVersion.current() >= JavaVersion.VERSION_13) {
        jvmArgs "-XX:+AllowRedefinitionToAddDeleteMethods"
    }
}
